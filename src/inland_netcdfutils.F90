#include "inland_config.h"

! ies-io.f last update 9/9/98 Christine C. Molling, cmolling@facstaff.wisc.edu
! University of Wisconsin - Madison; Institute for Environmental Studies;
! Center for Climatic Research; Climate, People, and Environment Program
!
! You are free to change or distribute my code as long as you
! 1) acknowlege me, Christine C. Molling (cmolling@facstaff.wisc.edu)
! as the author of the original code and 
! 2) do not sell it.
! Of course if there is anything wrong with the code, or you somehow
! encounter damage to your reputation/wallet because of its use, you
! are forbidden to sue me or anyone else.
!
!     These subroutines can be used as a primary interface to the ies
! format netcdf files.  You can also use the slightly lower level functions
! found in ies.f or the low level netcdf commands (see the netcdf V3
! users guide).
!SUBROUTINE LIST:
! inifile - create a new file with dimensions and variables for
!  longitude, latitude, an optional 3rd real dimension, and time
!  Time must have units of days since a date!!!
! inifilec - create a new file with dimensions and variables for
!  longitude, latitude, a 3rd character-variable dimension, and time
!  Time must have units of days since a date!!!
! inivar - initialize a new variable
! writevar - write a 2-, 3-, or 4-dimensional hyperslab of data
! readvar - read a 2-, 3-, or 4-dimensional hyperslab of data
! openfile - open file with write permissions
! closefile - sync file to disk, close file
! ---------------------------------------------------------------------

subroutine openfile(idies, filen, ierror)
! ---------------------------------------------------------------------
      implicit none

!INPUT
!    idies - integer - id of netcdf file (generated by inifile or inifilec)
!
!OUTPUT
!     ierror - integer - error code = 0 if no error, < 0 if error occurred
      integer idies, ierror
      integer ierr
      character(*) filen
      
#ifdef GFORTRAN
#include <netcdf.inc>
#else /* GFORTRAN */
      include 'netcdf.inc'
#endif

     ierr = NF_OPEN(filen,NF_WRITE,idies)
     if (ierr .ne. NF_NOERR) then
        print *, 'Error in openfile'
        print *, NF_STRERROR(ierr)
        ierror = -1
        return
     end if

     return

end subroutine openfile


subroutine closefile(idies, ierror)
! ---------------------------------------------------------------------
      implicit none

!INPUT
!    idies - integer - id of netcdf file (generated by inifile or inifilec)
!
!OUTPUT
!     ierror - integer - error code = 0 if no error, < 0 if error occurred
      integer idies, ierror
      integer ierr
      
#ifdef GFORTRAN
#include <netcdf.inc>
#else /* GFORTRAN */
      include 'netcdf.inc'
#endif

!     Close file, sync to disk
!     ------------------------
     ierr = NF_CLOSE(idies)
     if (ierr .ne. NF_NOERR) then
        print *, 'Error in closefile'
        print *, NF_STRERROR(ierr)
        ierror = -1
        return
     endif

     return

end subroutine closefile


! this subroutine prints an error message and the netcdf string associated to 
! the error code given
subroutine netcdf_error (message,code)

  implicit none

#ifdef GFORTRAN
#include <netcdf.inc>
#else /* GFORTRAN */
      include 'netcdf.inc'
#endif

  character*(*) :: message
  integer code

  print *, message
  print *, code, NF_STRERROR(code)
  stop 1

end subroutine netcdf_error


! this subroutine copies variable or global attributes from one ds to another
subroutine netcdf_copyattrs(varname,natts,ids,ivarid,ods,ovarid)
  
  implicit none

  integer natts,ids,ivarid,ods,ovarid
  character*(*) :: varname

  integer i, ierr, attlen, xtype
  real*8   tmpreal(8196)
  integer  tmpint(8196)
  character*80 attname
  character*1024 tmpchar

#ifdef GFORTRAN
#include <netcdf.inc>
#else /* GFORTRAN */
  include 'netcdf.inc'
#endif

  do i = 1, natts
     ierr = NF_INQ_ATTNAME(ids, ivarid, i, attname)
     if (ierr .ne. NF_NOERR) call netcdf_error("error getting attname "//trim(varname),ierr)
     ierr = NF_INQ_ATT(ids, ivarid, attname, xtype, attlen)
     if (ierr .ne. NF_NOERR) call netcdf_error("error getting attribute "//trim(varname),ierr)

     ! get and copy attributes
     if ( ( xtype .eq. NF_FLOAT ) .or. ( xtype .eq. NF_DOUBLE ) ) then
        ierr = NF_GET_ATT_DOUBLE(ids, ivarid, attname, tmpreal)
        if (ierr .ne. NF_NOERR) call netcdf_error("error",ierr)
        ierr = NF_PUT_ATT_DOUBLE(ods, ovarid, trim(attname), xtype, attlen, tmpreal)
        if (ierr .ne. NF_NOERR) call netcdf_error("error "//trim(attname)//" = "//tmpchar,ierr)
     else if ( xtype .eq. NF_CHAR ) then
        ierr = NF_GET_ATT_TEXT(ids, ivarid, attname, tmpchar)
        if (ierr .ne. NF_NOERR) call netcdf_error("error",ierr)
        ierr = NF_PUT_ATT_TEXT(ods, ovarid, trim(attname),attlen, trim(tmpchar))
        if (ierr .ne. NF_NOERR) call netcdf_error("error "//trim(attname)//" = "//tmpchar,ierr)
     else ! others - assume int (not tested)
        ierr = NF_GET_ATT_INT(ids, ivarid, attname, tmpint)
        ierr = NF_PUT_ATT_INT(ods, ovarid, trim(attname), xtype, attlen, tmpint)
        if (ierr .ne. NF_NOERR) call netcdf_error("error "//trim(attname)//" = *",ierr)
     end if
  end do

end subroutine netcdf_copyattrs



module inland_netcdfutils

contains


integer function get_nodata_int(ds, varid)

  implicit none

  integer, intent(in) :: ds, varid

  integer ierr, vartype
  character*1024 tmpchar

#ifdef GFORTRAN
#include <netcdf.inc>
#else /* GFORTRAN */
  include 'netcdf.inc'
#endif

  get_nodata_int = 0
  
  ierr = NF_INQ_VARTYPE(ds, varid, vartype)
  if (ierr .ne. NF_NOERR) call netcdf_error("error getting vartype in get_nodata",ierr)

  ierr = NF_GET_ATT_INT(ds, varid, "missing_value", get_nodata_int)
  if (ierr .ne. NF_NOERR) then
     ierr = NF_GET_ATT_DOUBLE(ds, varid, "_FillValue", get_nodata_int)
     if (ierr .ne. NF_NOERR) then
        if ( vartype .eq. NF_INT ) then
            get_nodata_int = NF_FILL_INT
         else if ( vartype .eq. NF_SHORT ) then
            get_nodata_int = NF_FILL_SHORT
         else if ( vartype .eq. NF_BYTE ) then
            get_nodata_int = NF_FILL_BYTE
         else
            get_nodata_int = 0
         end if
        print *, "ERROR getting nodata for ds", ds, "varid", varid, " : "
        print *, NF_STRERROR(ierr)
     end if
  end if

end function get_nodata_int

 
real*8 function get_nodata_real(ds, varid)

  implicit none

  integer, intent(in) :: ds, varid

  integer ierr, vartype
  character*1024 tmpchar

#ifdef GFORTRAN
#include <netcdf.inc>
#else /* GFORTRAN */
  include 'netcdf.inc'
#endif

  get_nodata_real = 0.
  
  ierr = NF_INQ_VARTYPE(ds, varid, vartype)
  if (ierr .ne. NF_NOERR) call netcdf_error("error getting vartype in get_nodata",ierr)

  ierr = NF_GET_ATT_DOUBLE(ds, varid, "missing_value", get_nodata_real)
  if (ierr .ne. NF_NOERR) then
     ierr = NF_GET_ATT_DOUBLE(ds, varid, "_FillValue", get_nodata_real)
     if (ierr .ne. NF_NOERR) then
        get_nodata_real = NF_FILL_DOUBLE
        if ( vartype .eq. NF_FLOAT ) get_nodata_real = NF_FILL_FLOAT
        print *, "ERROR getting nodata for ds", ds, "varid", varid, " : "
        print *, NF_STRERROR(ierr)
     end if
  end if

end function get_nodata_real


end module inland_netcdfutils
